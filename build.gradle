plugins {
  id "com.github.ben-manes.versions" version "0.42.0"
  id 'maven-publish'
  id 'signing'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.9.1'
ext.desc = 'Calimero, a free KNX network library'

apply plugin: 'java-library'
apply plugin:'application'

group = 'com.github.calimero'
version = '2.6-SNAPSHOT'

sourceCompatibility = 19
targetCompatibility = 19

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaExec) { jvmArgs += "--enable-preview" }

tasks.withType(Jar) {
	from("${projectDir}") {
        include 'LICENSE.txt'
        into "META-INF"
    }
    if (name.equals('sourcesJar')) {
    	from("${projectDir}") {
		    include 'README.md'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'


compileJava.options*.compilerArgs = [
	'-Xlint:all,-serial',
	"--limit-modules", "java.base,java.xml",
	"--enable-preview"
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]


mainClassName = 'tuwien.auto.calimero.Settings'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['test']
        java.exclude 'resources/', '**/.gradle'
        resources.srcDirs = ['test/resources']
    }
}

tasks.withType(Javadoc) {
    options.addStringOption("Xdoclint:-missing", '-quiet')
    options.addBooleanOption('-enable-preview', true)
	options.addStringOption("-release", "${targetCompatibility}")
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

test {
	useJUnitPlatform() {
		excludeTags 'ft12', 'slow'
//		excludeTags 'knxnetip'
		testLogging {
//			exceptionFormat "full"
//			showStandardStreams true
		}
	}
	jvmArgs(['--enable-preview'])
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero core library (Loom)'
                description = 'Calimero, a free KNX network library'
                url = 'https://github.com/calimero-project/calimero-core'
                inceptionYear = '2006'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:calimero-project/calimero-core.git'
                    url = 'https://github.com/calimero-project/calimero-core.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}

dependencies {
    api 'org.slf4j:slf4j-api:1.7.36'
    api 'org.usb4java:usb4java-javax:1.3.0'

	testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
	testCompileOnly 'junit:junit:4.13.2'
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
}
